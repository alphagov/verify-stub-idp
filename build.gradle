buildscript {
    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/allowed-repos for production builds.\n\n')
            maven { url 'https://dl.bintray.com/alphagov/maven-test' }
            jcenter()
        }
        else {
            maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
        }
    }
    dependencies {
      classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

nexusPublishing {
    useStaging = true
    repositories {
        sonatype {
            // because we registered in Sonatype after 24 Feb 2021, we provide these URIs
            // see: https://github.com/gradle-nexus/publish-plugin/blob/master/README.md
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

ext {
  opensaml_version = '3.4.3'
  build_version = "$opensaml_version-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
}

def dependencyVersions = [
        opensaml              : "$opensaml_version",
        ida_utils             : '2.0.0-392',
        dropwizard            : '1.3.8',
        ida_dev_pki           : '1.1.0-37',
        saml_libs             : "$opensaml_version-250",
        ida_test_utils        : '2.0.0-46',
        hub_saml              : "$opensaml_version-15766",
        hk2_version           : '2.5.0-b05'
]

group = "uk.gov.ida"
version = "${build_version}"

subprojects {
    apply plugin: 'java'

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/allowed-repos for production builds.\n\n')
            maven { url 'https://dl.bintray.com/alphagov/maven' } // For dropwizard-logstash
            maven { url 'https://dl.bintray.com/alphagov/maven-test' } // For other public verify binaries
            maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
            jcenter()
        }
        else {
            maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
        }
    }

    configurations {
        stub_idp
        stub_idp_runtime
        stub_idp_test
        stub_idp_saml
        stub_idp_saml_test
    }

    dependencies {
        stub_idp 'com.hubspot.dropwizard:dropwizard-guicier:1.0.0.6',
                // 0-jersey2-guice-spi has to be near the top of the dependencies
                'com.squarespace.jersey2-guice:0-jersey2-guice-spi:1.0.6',
                'de.neuland-bfi:jade4j:0.4.0',
                "io.dropwizard:dropwizard-assets:$dependencyVersions.dropwizard",
                "io.dropwizard:dropwizard-auth:$dependencyVersions.dropwizard",
                "io.dropwizard:dropwizard-client:$dependencyVersions.dropwizard",
                "io.dropwizard:dropwizard-configuration:$dependencyVersions.dropwizard",
                "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard",
                "io.dropwizard:dropwizard-metrics-graphite:$dependencyVersions.dropwizard",
                "io.dropwizard:dropwizard-views:$dependencyVersions.dropwizard",
                "io.dropwizard:dropwizard-views-freemarker:$dependencyVersions.dropwizard",
                "org.flywaydb:flyway-core:4.2.0",
                'org.freemarker:freemarker:2.3.27-incubating',
                "org.jdbi:jdbi3-core:3.0.0",
                'org.jsoup:jsoup:1.6.1',
                'org.mindrot:jbcrypt:0.3m',
                "org.opensaml:opensaml-core:$dependencyVersions.opensaml",
                "org.postgresql:postgresql:42.1.4",
                "uk.gov.ida:common-utils:$dependencyVersions.ida_utils",
                "uk.gov.ida:saml-lib:$dependencyVersions.saml_libs",
                "uk.gov.ida:rest-utils:$dependencyVersions.ida_utils",
                "uk.gov.ida:security-utils:$dependencyVersions.ida_utils",
                "javax.activation:javax.activation-api:1.2.0",
                "javax.xml.bind:jaxb-api:2.2.3",
                project(':stub-idp-saml')

        stub_idp_runtime 'joda-time:joda-time:2.3',
                'net.logstash.logback:logstash-logback-encoder:4.9',
                'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.9.5'

        stub_idp_test "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard",
                "uk.gov.ida:ida-dev-pki:$dependencyVersions.ida_dev_pki",
                "uk.gov.ida:saml-test:$dependencyVersions.saml_libs",
                "uk.gov.ida:common-test-utils:$dependencyVersions.ida_test_utils",
                "uk.gov.ida:hub-saml:$dependencyVersions.hub_saml",
                "uk.gov.ida:hub-saml-test-utils:$dependencyVersions.hub_saml",
                "com.h2database:h2:1.4.196",
                'commons-codec:commons-codec:1.10',
                project(':stub-idp-saml-test')

        stub_idp_saml  "uk.gov.ida:saml-lib:$dependencyVersions.saml_libs",
                "org.opensaml:opensaml-core:$dependencyVersions.opensaml",
                'joda-time:joda-time:2.9',
                'javax.inject:javax.inject:1',
                'javax.validation:validation-api:1.1.0.Final',
                'com.fasterxml.jackson.core:jackson-annotations:2.6.0',
                'org.slf4j:slf4j-api:1.7.12',
                'commons-lang:commons-lang:2.6'

        stub_idp_saml_test 'commons-codec:commons-codec:1.10',
                "uk.gov.ida:ida-dev-pki:$dependencyVersions.ida_dev_pki",
                "uk.gov.ida:saml-test:$dependencyVersions.saml_libs",
                "uk.gov.ida:common-utils:$dependencyVersions.ida_utils",
                'org.assertj:assertj-joda-time:1.1.0'
    }
}

task(outputDependencies) {
    doLast {
        println "saml_lib=" + dependencyVersions.saml_libs
        println "ida_utils=" + dependencyVersions.ida_utils
    }
}

defaultTasks 'clean', 'test', 'jar'
